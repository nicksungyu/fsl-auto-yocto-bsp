#@TYPE: Machine
#@NAME: NXP BlueBox Mini
#@SOC: LSCH3
#@DESCRIPTION: Machine configuration for the BlueBox Mini
#@MAINTAINER: Heinz Wrobel <Heinz.Wrobel@nxp.com>

require conf/machine/include/ls208xa-base.inc

SOC_FAMILY:prepend = "ls2084a:"

MACHINE_FEATURES += "optee"

RCWNOR ?= "FFFFPPHH_0x2a_0x41/rcw_ffffpphh_0x2a_0x41_1800_700_1867_1600"
RCWSD ?= "${@bb.utils.contains('DISTRO_FEATURES', 'ls2-pcie-ep', \
       'FFFFPPHH_0x2a_0x41/rcw_ffffpphh_0x2a_0x41_SDHC_EPGen2_1800_700_1867_1600', \
       'FFFFPPHH_0x2a_0x41/rcw_ffffpphh_0x2a_0x41_SDHC_1800_700_1867_1600', d)}"

UBOOT_CONFIG ??= "tfa"
UBOOT_CONFIG[tfa] = "${MACHINE}_tfa_defconfig,,u-boot-dtb.bin"

# For the factory image, we support a special environment
UBOOT_ENV_NAME:append:ls2084abbmini = " u-boot-flashenv-factory"

UBOOT_ENV_INC_NAME:append:fsl-lsch3 = " distro_boot"

KERNEL_DEVICETREE ?= "freescale/fsl-ls2084a-bbmini.dtb"
KERNEL_DEFCONFIG ?= "defconfig"

SERIAL_CONSOLES ?= "115200;ttyS0 115200;ttyS1"
SERIAL_CONSOLES_CHECK ?= "${SERIAL_CONSOLES}"

MACHINE_EXTRA_RRECOMMENDS = ""

USE_VT = "0"

# On the BlueBox we have a 128MB NOR flash for LS2 and Bank 4 support.
# Given modern kernel sizes, we do not use virtual/kernel but a
# size reduced version suitable for a recovery Linux in flash.
require include/lxflashmap.inc
FLASHIMAGE_KERNEL = "linux-flash"
FLASHIMAGE_KERNEL_BASENAME = "kernel-flash"
FLASHIMAGE_SIZE = "128"
FLASHIMAGE_BANK4 = "yes"
FLASHIMAGE_RESET = "${ATF}"
FLASHIMAGE_RESET_FILE = "${ATF_RESET_FILE_NOR}"
FLASHIMAGE_EXTRA9 = "linux-kernelitb"
FLASHIMAGE_EXTRA9_FILE = "kernel-flash-withrootfs.itb"
FLASHIMAGE_EXTRA9_OFFSET = "${FLASHIMAGE_KERNEL_OFFSET}"
FLASHIMAGE_EXTRA1 = "management-complex"
FLASHIMAGE_EXTRA1_FILE = "mc_app/mc.itb"
FLASHIMAGE_EXTRA1_OFFSET = "${FLASHIMAGE_DPAA2_MC_OFFSET}"
FLASHIMAGE_EXTRA2 = "mc-utils"
FLASHIMAGE_EXTRA2_FILE = "mc-utils/custom/${MACHINE}-dpl-ethbluebox.0x2A_0x41.dtb"
FLASHIMAGE_EXTRA2_OFFSET = "${FLASHIMAGE_DPAA2_DPL_OFFSET}"
FLASHIMAGE_EXTRA3 = "mc-utils"
FLASHIMAGE_EXTRA3_FILE = "mc-utils/custom/${MACHINE}-dpc-0x2a41.dtb"
FLASHIMAGE_EXTRA3_OFFSET = "${FLASHIMAGE_DPAA2_DPC_OFFSET}"
FLASHIMAGE_EXTRA4 = "u-boot-environment"
FLASHIMAGE_EXTRA4_FILE = "u-boot-flashenv-${MACHINE}.bin"
FLASHIMAGE_EXTRA4_OFFSET = "${FLASHIMAGE_UBOOT_ENV_OFFSET}"
FLASHIMAGE_UBOOT = "${ATF}"
FLASHIMAGE_UBOOT_FILE = "${ATF_IMAGE_FILE_NOR}"

# Generating an SDHC image to be directly booted with RCW=0x40
SDCARD_RCW = "${ATF}"
SDCARD_RCW_NAME = "${ATF_RESET_FILE_SD}"

SDCARD_BINARY_SPACE = "65536"
UBOOT_NAME_SDCARD = "${ATF_IMAGE_FILE_SD}"
UBOOT_BOOTSPACE_OFFSET  = "${FLASHIMAGE_UBOOT_OFFSET}"
BOOT_SPACE ?= "65536"

# No emergency Ubuntu Linux on the SD card as flash like element,
# so we disable inheritance from above by explicitly clearing it.
SDCARDIMAGE_EXTRA9_FILE:ubuntu = ""

IMAGE_FSTYPES += "sdcard"
